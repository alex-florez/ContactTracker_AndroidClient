plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    id 'com.google.secrets_gradle_plugin' version '0.5'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.gms.google-services'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.2"

    defaultConfig {
        applicationId "es.uniovi.eii.contacttracker"
        minSdkVersion 26
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
//        testInstrumentationRunner "es.uniovi.eii.contacttracker.HiltTestRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "BASE_URL", "\"http://10.0.2.2:8080\""
        }

        // Variante DEBUG con las peticiones realizadas en LOCAL.
        debugLocalhost {
            initWith debug
            buildConfigField "String", "BASE_URL", "\"http://10.0.2.2:8080\""
        }

        // Variante DEBUG con las peticiones realizadas al servidor DESPLEGADO.
        debugDeploy {
            initWith debug
            buildConfigField "String", "BASE_URL", "\"https://contacttrackerbackend.azurewebsites.net\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }

    useLibrary("android.test.runner")

    packagingOptions {
        exclude "**/attach_hotspot_windows.dll"
        exclude "META-INF/licenses/**"
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude("META-INF/*.kotlin_module")
        exclude('META-INF/AL2.0')
        exclude('META-INF/LGPL2.1')
    }
}

dependencies {
    // Dependencias por defecto al crear el proyecto
    implementation "org.jetbrains.kotlin:kotlin-stdlib:1.5.0"
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.wear:wear:1.0.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    // Testing
    androidTestImplementation('androidx.test:core:1.4.0')
    // AndroidJUnitRunner and JUnit Rules
    androidTestImplementation('androidx.test:runner:1.4.0')
    androidTestImplementation('androidx.test:rules:1.4.0')
    androidTestImplementation('androidx.test.ext:truth:1.4.0')
    androidTestImplementation('com.google.truth:truth:1.0.1')
    // Testing Kotlin Corroutines
    androidTestImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.0'
    // Instant Task Executor para los LiveData
    testImplementation 'androidx.arch.core:core-testing:2.1.0'
    androidTestImplementation 'androidx.arch.core:core-testing:2.1.0'

    // Robolectric
    testImplementation 'org.robolectric:robolectric:4.5.1'

    // Mockito
    testImplementation 'org.mockito:mockito-core:3.12.4'
    androidTestImplementation 'com.linkedin.dexmaker:dexmaker-mockito-inline:2.28.0'



    // For Robolectric tests.
    testImplementation("com.google.dagger:hilt-android-testing:$hilt_version")
    // ...with Kotlin.
    kaptTest("com.google.dagger:hilt-android-compiler:$hilt_version")

    // For instrumented tests.
    androidTestImplementation("com.google.dagger:hilt-android-testing:$hilt_version")
    // ...with Kotlin.
    kaptAndroidTest("com.google.dagger:hilt-android-compiler:$hilt_version")


    /**
     * Dependencias EXTRA
     */
    def activity_version = "1.2.4"
    compileOnly 'com.google.android.wearable:wearable:2.6.0'
    def fragment_version = "1.3.5"
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation "androidx.activity:activity-ktx:$activity_version"


    // Localización
    implementation 'com.google.android.gms:play-services-location:18.0.0'

    // ROOM
    implementation 'androidx.room:room-runtime:2.3.0'
    kapt "androidx.room:room-compiler:2.3.0"
    implementation "androidx.room:room-ktx:2.3.0"

    // Material Design
    implementation 'com.google.android.material:material:1.3.0'

    // Android Jetpack Navigation
    def nav_version = "2.3.5"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // ViewPager 2
    implementation "androidx.viewpager2:viewpager2:1.0.0"

    // Corrutinas
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"

    // Hilt: Inyección de dependencias
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    // Retrofit & GSON
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'

    // Google Maps SDK
    implementation 'com.google.android.gms:play-services-maps:17.0.1'

    // Firebase BoM
//    implementation platform('com.google.firebase:firebase-bom:28.4.0')

    // Firebase Google Analytics
    implementation 'com.google.firebase:firebase-analytics-ktx:19.0.1'
    implementation 'com.google.firebase:firebase-messaging-ktx:22.0.0'

}